plugins {
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.dokka' version '1.4.20'
}

repositories {
    jcenter()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

task javadocJar(type: Jar) {
    from dokkaJavadoc
    classifier 'javadoc'
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "https://repo.bristermitten.me/repository/maven-releases/"
            def snapshotsRepoUrl = "https://repo.bristermitten.me/repository/maven-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username project.hasProperty('maven.username') ? project.property('maven.username') : System.getenv('MAVEN_USERNAME')
                password project.hasProperty('maven.password') ? project.property('maven.password') : System.getenv('MAVEN_PASSWORD')
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            group 'org.kryptonmc'
            artifactId = 'krypton-api'
            version globalVersion

            from components.kotlin
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Krypton API'
                description = 'The official API for Krypton, the fast and lightweight Minecraft server written in Kotlin!'
                url = 'https://www.kryptonmc.org'

                packaging = 'jar'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'bombardygamer'
                        name = 'Callum Seabrook'
                        email = 'callum.seabrook@prevarinite.com'
                        timezone = 'Europe/London'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/KryptonMC/Krypton.git'
                    developerConnection = 'scm:git:ssh://github.com:KryptonMC/Krypton.git'
                    url = 'https://github.com/KryptonMC/Krypton'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}